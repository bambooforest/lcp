?start: _NL* [(query|results)+]

query           : statement__

statement__     : "sequence" label? repetition? _NL _INDENT members+ _DEDENT          -> sequence
                | "set" label? _NL _INDENT members+ _DEDENT                           -> set
                | "group" label? _NL _INDENT members+ _DEDENT                         -> group
                | quantor__universal _NL _INDENT args__q_two _DEDENT                  -> universal_quantification
                | quantor__existential _NL _INDENT args__q_one _DEDENT                -> existential_quantification
                | operator__unary _NL _INDENT args__one _DEDENT                       -> logical_op_unary
                | operator__binary _NL _INDENT args__two _DEDENT                      -> logical_op_binary
                | operator__n_ary _NL _INDENT args__any _DEDENT                       -> logical_op_n_ary
                | layer("@"part_of)? label? _NL [_INDENT constraints+ _DEDENT]        -> unit

members         : statement__+                                                        -> members

constraints     : comparison
                | statement__

quantor__universal   : QUANTOR_UNIVERSAL
quantor__existential : QUANTOR_EXISTENTIAL

operator__unary  : OPERATOR_UNARY
operator__binary : OPERATOR_BINARY
operator__n_ary  : OPERATOR_N_ARY

args__q_one      : statement__ ~ 1
args__q_two      : statement__ ~ 2
args__one        : constraints ~ 1
args__two        : constraints ~ 2
args__any        : constraints constraints+

results         : label RESULTS_ARROW results_type__                                 -> results
results_type__  : "plain" _NL _INDENT context entities _DEDENT                       -> results_plain
                | "analysis" _NL _INDENT attributes filter? functions _DEDENT        -> results_analysis
                | "collocation" _NL _INDENT (center window|space) attribute _DEDENT  -> results_collocation

context         : "context" _NL _INDENT label _NL* _DEDENT _NL*                      -> context
entities        : "entities" _NL _INDENT entity_ref__+ _NL* _DEDENT _NL*             -> entities

attributes      : "attributes" _NL _INDENT (attribute__ _NL)+ _DEDENT                -> attributes
filter          : "filter" _NL _INDENT (filter__ _NL)+ _DEDENT                       -> filter
functions       : "functions" _NL _INDENT functions__+ _DEDENT                       -> functions

center          : "center" _NL _INDENT entity_ref__ _DEDENT                          -> center
window          : "window" _NL _INDENT range__ _DEDENT                               -> window
space           : "space" _NL _INDENT entity_ref__+ _DEDENT                          -> space
attribute       : "attribute" _NL _INDENT attribute__ _DEDENT                        -> attribute

comparison      : entity operator comparison_type__ _NL
entity          : VARIABLE
operator        : OPERATOR

comparison_type__ : NUMBER_EXPRESSION                                                -> math_comparison
                  | STRING_LITERAL                                                   -> string_comparison
                  | REGEX                                                            -> regex_comparison
                  | VARIABLE                                                         -> entity_comparison

label           : VARIABLE                                                             -> label
layer           : VARIABLE                                                             -> layer
part_of         : VARIABLE                                                             -> part_of
repetition      : REPETITION                                                           -> repetition


entity_ref__    : VARIABLE _NL*

functions__     : ENUM_FUNCTIONS
attribute__     : STRING
filter__        : STRING
range__         : RANGE

VARIABLE        : /[a-zA-Z_][a-zA-Z0-9_\.]*/
OPERATOR.1      : /(<>|>=|<=|<|>|!=|¬=|¬~|~|¬|=|!|in)/
STRING_LITERAL  : /('.+'|".+")/
REGEX           : /\/.+\//
NUMBER_EXPRESSION.1 : /^[(]*([a-zA-Z][a-zA-Z0-9]*|-?([0-9]+[.])?[0-9]+[smy]?)( +[*\/+-] +[(]*([a-zA-Z][a-zA-Z0-9]*|-?([0-9]+[.])?[0-9]+[smy]?))?[)]*$/
RANGE           : /([-+][0-9]+|0)[.]{2}([-+][0-9]+|0)/

ENUM_FUNCTIONS  : /frequency|minimum|maximum|average|stddev/

QUANTOR_UNIVERSAL.1   : /(ALL|!ALL|¬ALL)/
QUANTOR_EXISTENTIAL.1 : /(EXIST|!EXIST|¬EXIST)/

OPERATOR_UNARY.1  : /NOT/
OPERATOR_BINARY.1 : /(NAND|!AND|¬AND|NOR|!OR|¬OR|XOR|IMPLY|NIMPLY|!IMPLY|¬IMPLY)/
OPERATOR_N_ARY.1  : /(AND|OR)/

RESULTS_ARROW.2 : /=>/
BOOLEAN.1       : /(AND|OR)/
REPETITION.1    : /(\d+\.\.(\d+|\*)|[1-9]+\d*)/
STRING          : /[^\n\r ].*/
SL_COMMENT      : /#[^\r\n]+/ _NL
DL_COMMENT      : /<#(>|#*[^#>]+)*#+>/ _NL

%import common.CNAME                                                                -> NAME
%import common.WS_INLINE
%declare _INDENT _DEDENT
%ignore WS_INLINE
%ignore SL_COMMENT
%ignore DL_COMMENT

_NL: /(\r?\n[\t ]*)+/
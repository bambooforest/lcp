?start: _NL* [(query|results)+]

query           : statement__

statement__     : SEQUENCE("@"part_of)? label? repetition? _NL _INDENT members+ _DEDENT       -> sequence
                | SET label? _NL _INDENT members+ _DEDENT                                     -> set
                | GROUP label? _NL _INDENT members+ _DEDENT                                   -> group
                | quantor__universal _NL _INDENT args__q_two _DEDENT                          -> universal_quantification
                | quantor__existential _NL _INDENT args__q_one _DEDENT                        -> existential_quantification
                | operator__unary _NL _INDENT args__one _DEDENT                               -> logical_op_unary
                | operator__binary _NL _INDENT args__two _DEDENT                              -> logical_op_binary
                | operator__n_ary _NL _INDENT args__any _DEDENT                               -> logical_op_n_ary
                | layer(":"partition)?("@"part_of)? label? _NL [_INDENT constraints+ _DEDENT] -> unit
                | "```cqp" _NL _INDENT cqp__ _DEDENT "```" _NL                                -> cqp
                | LABEL _NL*                                                                  -> reference

cqp__           : (ANYTHING _NL|_INDENT cqp__ _DEDENT)+

members         : statement__                                                         -> members

comparison      : entity operator comparison_type__ _NL                               -> comparison

constraints     : comparison
                | statement__

quantor__universal   : QUANTOR_UNIVERSAL
quantor__existential : QUANTOR_EXISTENTIAL

operator__unary  : OPERATOR_UNARY
operator__binary : OPERATOR_BINARY
operator__n_ary  : OPERATOR_N_ARY

args__q_one      : statement__ ~ 1
args__q_two      : statement__ ~ 2
args__one        : constraints ~ 1
args__two        : constraints ~ 2
args__any        : constraints constraints+

results         : label__results RESULTS_ARROW results_type__                        -> results
results_type__  : "plain" _NL _INDENT context entities _DEDENT                       -> results_plain
                | "analysis" _NL _INDENT attributes functions filter? _DEDENT        -> results_analysis
                | "collocation" _NL _INDENT (center window|space) attribute _DEDENT  -> results_collocation

context         : "context" _NL _INDENT context__+ _NL* _DEDENT _NL*                 -> context
entities        : "entities" _NL _INDENT entity_ref__+ _NL* _DEDENT _NL*             -> entities

attributes      : "attributes" _NL _INDENT (attribute__ _NL)+ _DEDENT                -> attributes
filter          : "filter" _NL _INDENT comparison _DEDENT                              -> filter
functions       : "functions" _NL _INDENT functions__+ _DEDENT                       -> functions

center          : "center" _NL _INDENT entity_ref__ _DEDENT                          -> center
window          : "window" _NL _INDENT range__ _NL _DEDENT                               -> window
space           : "space" _NL _INDENT entity_ref__+ _DEDENT                          -> space
attribute       : "attribute" _NL _INDENT attribute__ _NL _DEDENT                        -> attribute

entity          : (LABEL|LAYER|DOTENTITY)
operator        : OPERATOR

comparison_type__ : NUMBER_EXPRESSION                                                -> math_comparison
                  | STRING_LITERAL                                                   -> string_comparison
                  | REGEX                                                            -> regex_comparison
                  | (LABEL|LAYER|DOTENTITY)                                          -> entity_comparison

label           : LABEL                                                                -> label
layer           : LAYER                                                                -> layer
part_of         : LABEL                                                                -> part_of
partition       : LABEL                                                                -> partition
repetition      : REPETITION                                                           -> repetition

label__results  : (LABEL|LAYER)

entity_ref__    : LABEL _NL*
context__       : LABEL _NL*

functions__     : ENUM_FUNCTIONS _NL*
attribute__     : STRING
filter__        : STRING
range__         : RANGE

GROUP.4               : /group/
SET.4                 : /set/
SEQUENCE.4            : /sequence/

QUANTOR_UNIVERSAL.4   : /(ALL|!ALL|¬ALL)/
QUANTOR_EXISTENTIAL.4 : /(EXIST|!EXIST|¬EXIST)/
OPERATOR_UNARY.4      : /NOT/
OPERATOR_BINARY.4     : /(NAND|!AND|¬AND|NOR|!OR|¬OR|XOR|IMPLY|NIMPLY|!IMPLY|¬IMPLY)/
OPERATOR_N_ARY.4      : /(AND|OR)/
RESULTS_ARROW.4       : /=>/
ENUM_FUNCTIONS.4      : /frequency|minimum|maximum|average|stddev/

DOTENTITY.3      : /[a-zA-Z_][a-zA-Z0-9_]*\.[a-zA-Z0-9_]+/

LAYER.2          : /[A-Z_][a-zA-Z0-9_]*/
LABEL.2          : /[a-z][a-zA-Z0-9_]*/

OPERATOR.1      : /(<(-|\d+(\.\.\d+)?)?>|>=|<=|<|>|!=|¬=|¬~|~|¬|=|!|in)/
NUMBER_EXPRESSION.1 : /[(]*([a-zA-Z][a-zA-Z0-9]*|-?([0-9]+[.])?[0-9]+[smy]?)( +[*\/+-] +[(]*([a-zA-Z][a-zA-Z0-9]*|-?([0-9]+[.])?[0-9]+[smy]?))?[)]*/
REPETITION.1    : /(\d+\.\.(\d+|\*)|[1-9]+\d*)/

STRING_LITERAL  : /('.+'|".+")/
REGEX           : /\/.+\//
RANGE           : /([-+][0-9]+|0)[.]{2}([-+][0-9]+|0)/
STRING          : /[^\n\r ].*/

DL_COMMENT      : /<#(>|#*[^#>]+)*#+>/ _NL

ANYTHING        : /.+/

%import common.CNAME                                                                -> NAME
%import common.WS_INLINE
%declare _INDENT _DEDENT
%ignore WS_INLINE
%ignore DL_COMMENT

_NL: /((#[^\r\n]*)?\r?\n[\t ]*)+/
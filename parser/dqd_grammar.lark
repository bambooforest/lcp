?start: _NL* [predicate+]

predicate       : "sequence"scope? label? _NL [_INDENT predicate+ _DEDENT]          -> sequence
                | "set" label? _NL [_INDENT predicate+ _DEDENT]                     -> set
                | NOT_OPERATOR? "EXISTS" _NL [_INDENT predicate+ _DEDENT]           -> exists
                | BOOLEAN _NL [_INDENT (property|predicate)+ _DEDENT]               -> boolean
                | layer1 scope? label? _NL [_INDENT (property|predicate)+ _DEDENT]  -> layer_def
                | label "=> plain" _NL [_INDENT r_plain_prop+ _DEDENT]              -> results_plain
                | label "=> analysis" _NL [_INDENT r_analysis_prop+ _DEDENT]        -> results_analysis
                | label "=> collocation" _NL [_INDENT r_coll_prop+ _DEDENT]         -> results_collocation
property        : VARIABLE OPERATOR STRING _NL                                      -> property
repeat_loop     : /[\d+\*](\.\.[\d+\-\*])?/                                         -> loop
label           : VARIABLE                                                          -> label
layer1          : VARIABLE                                                          -> layer1
scope           : "@"VARIABLE                                                       -> scope
result_variable : VARIABLE _NL
r_plain_prop    : "context" _NL [_INDENT (label _NL)+ _DEDENT]                      -> result_plain_context
                | "entities" _NL [_INDENT (label _NL)+ _DEDENT]                     -> result_plain_entities
r_analysis_prop : "attributes" _NL [_INDENT (STRING _NL)+ _DEDENT]                  -> result_analysis_attributes
                | "functions" _NL [_INDENT (STRING _NL)+ _DEDENT]                   -> result_analysis_functions
                | "filter" _NL [_INDENT (STRING _NL)+ _DEDENT]                      -> result_analysis_filter
r_coll_prop     : "center" _NL [_INDENT (STRING _NL)+ _DEDENT]                      -> result_coll_center
                | "window" _NL [_INDENT (STRING _NL)+ _DEDENT]                      -> result_coll_window
                | "attribute" _NL [_INDENT (STRING _NL)+ _DEDENT]                   -> result_coll_attribute
                | "space" _NL [_INDENT (STRING _NL)+ _DEDENT]                       -> result_coll_space
                | "comment" _NL [_INDENT (STRING _NL)+ _DEDENT]                     -> result_coll_comment

VARIABLE        : /[a-zA-Z_][a-zA-Z0-9_\.]*/
NOT_OPERATOR.1  : /(¬|NOT|!)\s?/
BOOLEAN.1       : /(AND|OR)/
OPERATOR.1      : /(<>|>=|<=|<|>|!=|¬=|¬~|~|¬|=|!|in)/
STRING          : /[^\n\r ].*/
SL_COMMENT      : /#[^\r\n]+/ _NL
DL_COMMENT      : /<#(>|#*[^#>]+)*#+>/ _NL

%import common.CNAME                                                                -> NAME
%import common.WS_INLINE
%declare _INDENT _DEDENT
%ignore WS_INLINE
%ignore SL_COMMENT
%ignore DL_COMMENT

_NL: /(\r?\n[\t ]*)+/
